buildscript {
    apply from: project.rootProject.relativePath('buildscript.gradle'), to: buildscript
}

apply plugin: 'neva.osgi-container'

defaultTasks 'createOsgiContainer'
description = 'Sample application that can be used as quick start for using Javarel Framework'

allprojects {
    group 'com.neva.javarel'
    version "1.0.0.ALPHA"

    apply plugin: 'idea'
    apply plugin: 'base'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

dependencies {
    osgiModule project(':app:core')
    osgiModule project(':app:adm')
}

osgiContainer {
    felix()
    debug(16661)
    later(["javarel-*", "quickstart-*"])
    javaArg('-Dderby.system.home=db')
    exclude([
            'slf4j-api-1.7.7*',
            'slf4j-log4j12-1.7.6*',
            'xml-apis-1.3*',
            'org.apache.felix.scr.annotations-*',
            'antlr',
            'animal-sniffer-annotations',
            'kxml2',
            'xmlpull',
    ])
}

subprojects { subProject ->

    plugins.withId 'kotlin', {
        // testCompile group: 'junit', name: 'junit', version: '4.11'

        jar.baseName = "quickstart-${subProject.name}-${subProject.version}"
        sourceCompatibility = 1.7
        targetCompatibility = 1.7

        def compileOptions = {
            options.encoding = "UTF-8"
        }

        compileJava compileOptions
        compileTestJava compileOptions
    }

    plugins.withId 'maven-publish', {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifactId jar.baseName
                    from components.java
                }
            }
        }
    }

    plugins.withId 'neva.osgi-module', {
        jar {
            manifest {
                name subProject.description
                instruction 'Bundle-Category', 'quickstart'
            }
        }
    }
}